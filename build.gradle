buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.5.0')
    }
    configurations.classpath {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    group "${groupName}"
    version "${currentVersion}"
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

artifactoryPublish.skip = true

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    dependencies {
        compile 'com.intellisrc:groovy-extend:2.0'  //Using groovy 2.5.7
        testCompile 'org.spockframework:spock-unitils:1.2-groovy-2.5'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
				artifact(file("$rootDir/gradle.properties"))
            }
        }
    }

    artifactory {
        contextUrl = "${repositoryURL}"
        publish {
            repository {
                repoKey = currentVersion.endsWith("SNAPSHOT") ? "${repositoryDevelop}" : "${repositoryStable}" // The Artifactory repository key to publish to
                username = "${artifactoryUser}" // The publisher user name
                password = "${artifactoryPassword}" // The publisher password
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                // Properties to be attached to the published artifacts.
                properties = ['qa.level': 'basic', 'dev.team' : 'etc']
                // Publish generated POM files to Artifactory (true by default)
                publishPom = true
            }
        }
        resolve {
            repository {
                repoKey =  "${repositoryResolve}" // The Artifactory repository key to publish to
                username = "${artifactoryUser}" // The publisher user name
                password = "${artifactoryPassword}" // The publisher password
            }
        }
    }
}

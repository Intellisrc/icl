import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

dependencies {
    def os = DefaultNativePlatform.currentOperatingSystem
    def arch = DefaultNativePlatform.currentArchitecture

    api project(':etc')
    api project(':net')
    //https://github.com/intellisrc/spark
    // We use Response, Request... so we use 'api' here:
    api 'com.intellisrc:spark-core:2.9.4-unofficial-4'

    //TODO: reimplement security (LoginService) :
    //implementation 'org.pac4j:spark-pac4j:2.1.0'
    //implementation 'org.pac4j:pac4j-http:2.1.0'

    // NOTE: To enable Brotli compression, you may need to add this to your project:
    def jvmbrotli = "0.2.0"
    api "com.nixxcode.jvmbrotli:jvmbrotli:$jvmbrotli"
    switch (true) {
        case os.windows && arch.i386:
            api "com.nixxcode.jvmbrotli:jvmbrotli-win32-x86:$jvmbrotli"
            break
        case os.windows && arch.amd64:
            api "com.nixxcode.jvmbrotli:jvmbrotli-win32-x86-amd64:$jvmbrotli"
            break
        case os.macOsX && arch.amd64:
            api "com.nixxcode.jvmbrotli:jvmbrotli-darwin-x86-amd64:$jvmbrotli"
            break
        case os.linux && arch.amd64:
            api "com.nixxcode.jvmbrotli:jvmbrotli-linux-x86-amd64:$jvmbrotli"
            break
        default:
            println "No suitable driver of Brotli found for current " +
                "OS (${os.displayName}) and architecture (${arch.displayName}). Brotli will be disabled."
    }
}